[[usage]]
== Usage

=== Policies

There are two kinds of policies:

- Function-based policies (referred to as policies)
- Declarative policies (based on https://github.com/metosin/malli[malli] schemas)

Policies are single-arity functions which must return either of the following
values:

- `true` (or truthy)
- `false` (or falsey)
- string, eg. `"error message"`
- a collection, eg. `["error message 1" "error message 2"]`

Returning a string or a non-empty collection from the function will always
result in a policy failure. For truthy/falsey values, the outcome depends on
the policy's type. An empty collection is regarded as a falsey value.

For *deny policies*, function must return `false` (or falsey) to succeed.  For
*allow policies*, function must return `true` (or truthy) to succeed.  For
*warn policies*, function must return `false` (or falsey) to succeed.

In `conjtest`, policy names (ie. defined using `def` or `defn`) must start with
either `deny-`, `allow-`, or `warn-`.

When `conjtest` is provided a policy file, it will gather every public var in
that file starting with `deny-`, `allow-`, or `warn-` and evaluate them.
Functions using this naming conventions will be evaluated, functions not using
this convention will be filtered out and not evaluated.

[cols="1,1,1"]
|===
|Policy type|Policy name (begins with)|Policy success

|Deny
|`deny-`
|`false` or falsey

|Allow
|`allow-`
|`true` or truthy

|Warn
|`warn-`
|`false` or falsey
|===

.Deny policy example
[source, clojure]
----
(ns policy)

(defn- deployment?
  [input]
  (= "Deployment" (:kind input)))

(defn deny-should-not-run-as-root
  [input]
  (let [name (-> input :metadata :name)]
    (when (and (deployment? input)
               (not (true? (get-in input
                                   [:spec
                                    :template
                                    :spec
                                    :securityContext
                                    :runAsNonRoot]))))
      (format "Containers must not run as root in Deployment \"%s\"" name))))
----

.Allow policy example
[source, clojure]
----
(ns policy)

(defn is-allowlisted?
  [allowlist x]
  (assert (coll? allowlist))
  (some? ((into #{} allowlist) x)))

(def ^:private allowlist ["hello-kubernetes"])

(defn allow-allowlisted-selector-only
  [input]
  (and (= "Service" (:kind input))
       (is-allowlisted? allowlist (get-in input [:spec :selector :app]))))
----

.Warn policy example
[source, clojure]
----
(ns policy)

(defn warn-when-not-port-80
  [input]
  (and (= "v1" (:apiVersion input))
       (= "Service" (:kind input))
       (not= 80 (-> input :spec :ports first :port))))
----

==== Declarative policies

It's possible to also write declarative policies using
https://github.com/metosin/malli[malli] schemas.

Declarative policies work much the same way as function-based policies except
that they are defined as malli schemas and have different rules for policy
success. Malli schemas are evaluated using
https://github.com/metosin/malli?tab=readme-ov-file#validation[`malli.core/validate`]
after which they are processed for any errors using
https://github.com/metosin/malli?tab=readme-ov-file#error-messages[`malli.core/explain`]
and
https://github.com/metosin/malli?tab=readme-ov-file#humanized-error-messages[`malli.error/humanize`].

[cols="1,1,1"]
|===
|Policy type|Policy name (begins with)|Policy success

|Deny
|`deny-`
|`malli.core/validate` returns `false`

|Allow
|`allow-`
|`malli.core/validate` returns `true`

|Warn
|`warn-`
|`malli.core/validate` returns `false`
|===

It's recommended to write allow policies when using declarative policies. Deny
and warn declarative policies are still supported. However, custom error
messages are not supported with deny or warn policies.

.Declarative policy https://github.com/ilmoraunio/conjtest/blob/main/examples/yaml/kubernetes/deployment.yaml[YAML] example
[source, clojure]
----
(ns policy)

(def allow-declarative-example
  [:map
   [:kind [:= "Deployment"]]
   [:metadata
    [:map
     [:name :string]]]
   [:spec
    [:map
     [:selector
      [:map
       [:matchLabels
        [:map
         [:app :string]
         [:release :string]]]]]
     [:template
      [:map
       [:spec
        [:map
         [:securityContext {:optional true}
          [:map
           [:runAsNonRoot [:not= {:error/message "Containers must not run as root"}
                           true]]]]]]]]]]])
----

.Declarative policy https://github.com/ilmoraunio/conjtest/tree/main/examples/json[JSON] example using regular expressions
[source, clojure]
----
(ns policy)

(def allow-declarative-example
  [:map
   [:dependencies [:map-of :keyword [:re
                                     {:error/fn (fn [{:keys [value]} _]
                                                  (format "caret ranges not allowed, version found: %s" value))}
                                     "^[0-9~]|latest|beta|>="]]]])
----

.Declarative policy https://github.com/ilmoraunio/conjtest/tree/main/examples/edn[EDN] example using Fn schemas
[source, clojure]
----
(def allow-declarative-example
  [:and
   [:map
    [:db
     [:map
      [:user :string]
      [:pwd :string]
      [:host :string]
      [:db :string]
      [:port :int]]]
    [:myapp [:map
             [:port :int]
             [:features [:set
                         [:enum :admin-panel :keyboard-shortcuts]]]
             [:foo
              [:map
               [:hostname :string]
               [:api-keys [:vector :string]]
               [:recheck-frequency :string]]]
             [:forever-date inst?]
             [:process-pool :int]]]
    [:log :keyword]
    [:env [:enum :production]]]
   [:fn {:error/message "Applications in the production environment should have error only logging"}
    (fn [input]
      (and (= :production (:env input))
           (= :error (:log input))))]])
----

More declarative policy examples
https://github.com/search?q=repo%3Ailmoraunio%2Fconjtest+%22def+allow-declarative-example%22&type=code[here].

=== Policy metadata

You can define policy name, top-level error message (if the rule otherwise
fails), and the policy type using metadata.

[cols="1,1,1"]
|===
|Supported keys|Supported values|Superseded by

| `rule/type` | `:deny`, `:allow`, `:warn` | Policy name (begins with `deny-`, `allow-`, or `warn-`)
| `rule/message` | string | Function returns an error message
|===

.Policy with custom name and error message
[source, clojure]
----
(defn ^{:rule/type :deny
        :rule/message "port should be 80"}
      differently-named-deny-rule
  [input]
  (and (= "v1" (:apiVersion input))
       (= "Service" (:kind input))
       (not= 80 (-> input :spec :ports first :port))))
----

=== Running policies

Once you have a configuration file and a policy, you can perform policy testing
using the following command syntax:

[source, bash]
----
conjtest test <configuration_file> [configuration_file [...]] [flags]
----

You can provide file(s) directly, use globs, or directories for both
configuration files and policies.

.Basic example
[source, bash]
----
conjtest test infra/deployment.yaml --policy policies/policy.clj
----

.Multiple files
[source, bash]
----
conjtest test infra/deployment.yaml infra/my-other-deployment.yml --policy policies/policy.clj --policy other_policies/another-policy.clj
----

.Glob support
[source, bash]
----
conjtest test infra/**/*.{yaml,yml} --policy **/*.clj
----

.Directories support
[source, bash]
----
conjtest test infra/ --policy policies/
----

=== Exit codes

`conjtest test` command normally returns exit code 0 on success. Failing deny
or allow policies result in exit code 1.  Warn policies will result in exit
code 0.

=== --fail-on-warn

If the option `--fail-on-warn` is provided to the `conjtest test` command, then
failing warn policies result in error code 1. Failing deny policies will result
in exit code 2.

=== Supported runtime libraries

Policies are run using Babashka/Clojure runtime. Conjtest runs policies inside
a https://github.com/babashka/sci[SCI] sandbox which places limitations on code
that can be run.

Currently, **only** the following namespaces (and their contents) can be
required:

- `clojure.core`
- `clojure.set`
- `clojure.edn`
- `clojure.repl`
- `clojure.string`
- `clojure.walk`
- `clojure.template`
- Locally defined namespaces (see: xref:_local_file_requires[])

=== Keyworded keys

[sidebar]
Since 0.2.0 this behavior is provided by default when running `conjtest init`.

Some configuration formats support keyworded keys by default.

.CLI invocation example
[source, bash]
----
$ conjtest parse deps.edn

{:deps
 {org.clojure/clojure #:mvn{:version "1.12.0"},
  ilmoraunio/conjtest-clj #:local{:root "../conjtest-clj"}}}
----

To return keyworded keys for all configuration formats, you can provide the
`keywordize?` option via `--config`.

.conjtest.edn
[source, clojure]
----
{:keywordize? true}
----

.CLI invocation example
[source, bash]
----
$ conjtest parse examples/hcl2/terraform.tf --config conjtest.edn
{:resource
 {:aws_alb_listener
  {:my-alb-listener [{:port "80", :protocol "HTTP"}]},
  :aws_db_security_group {:my-group [{}]},
  :aws_s3_bucket
  {:valid
   [{:acl "private",
     :bucket "validBucket",
     :tags {:environment "prod", :owner "devops"}}]},
  :aws_security_group_rule
  {:my-rule [{:cidr_blocks ["0.0.0.0/0"], :type "ingress"}]},
  :azurerm_managed_disk
  {:source [{:encryption_settings [{:enabled false}]}]}}}

$ conjtest test examples/hcl2/terraform.tf -p examples/hcl2/policy.clj --config conjtest.edn
FAIL - examples/hcl2/terraform.tf - deny-fully-open-ingress - ASG rule ':my-rule' defines a fully open ingress
FAIL - examples/hcl2/terraform.tf - deny-http - ALB listener ':my-alb-listener' is using HTTP rather than HTTPS
FAIL - examples/hcl2/terraform.tf - deny-missing-tags - AWS resource: :aws_alb_listener named ':my-alb-listener' is missing required tags: #{:environment :owner}
FAIL - examples/hcl2/terraform.tf - deny-missing-tags - AWS resource: :aws_db_security_group named ':my-group' is missing required tags: #{:environment :owner}
FAIL - examples/hcl2/terraform.tf - deny-missing-tags - AWS resource: :aws_security_group_rule named ':my-rule' is missing required tags: #{:environment :owner}
FAIL - examples/hcl2/terraform.tf - deny-unencrypted-azure-disk - Azure disk ':source' is not encrypted

4 tests, 0 passed, 0 warnings, 4 failures
----

=== Local file requires

It's possible to require your locally defined namespaces by passing a
configuration file via `--config` and `:paths` key.

.CLI invocation example
[source, bash]
----
conjtest test infra/deployment.yaml --policy policies/policy.clj --config conjtest.edn
----

.conjtest.edn
[source, clojure]
----
{:paths ["policies/"]}
----

Conjtest will will recursively include namespaces of all `.clj`, `.cljc`, and
`.bb` files.

Now you can require any local Clojure namespaces.

.Policy example using local require
[source, clojure]
----
(ns conjtest.example-local-require
  (:require [conjtest.util.core :as util]))

(def ^:private allowlist ["hello-kubernetes"])

(defn allow-allowlisted-selector-only
  [input]
  (and (= "Service" (:kind input))
       (util/is-allowlisted? allowlist (get-in input [:spec :selector :app]))))
----

=== Parsing configuration files

Conjtest uses multiple parsers under-the-hood to provide wide support for
different configuration file formats, using either Clojure-based parsers or
Go-based (ie. https://conftest.dev[Conftest]-based) parsers.

Conjtest will try to parse using Clojure-based parsers if a suitable parser can
be found, otherwise it will use a Go-based parser. If a suitable parser cannot
be found, exit code 1 is returned.

Conjtest supports returning keys using keywords for Clojure-based parsers. For
Go-based parsers, keys are returned as a string by default, but can be changed
via configuration file (see: xref:_keyworded_keys[]).

If necessary, you can force Conjtest to use Go-based parsers via
`--go-parsers-only`.

==== Supported configuration file formats
- https://github.com/ilmoraunio/conjtest/tree/main/examples/cue[CUE]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/dockerfile[Dockerfile]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/dotenv[Dotenv]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/edn[EDN]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/hcl1[HCL1]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/hcl2[HCL2]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/hocon[HOCON]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/ignore[Ignore]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/ini[INI]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/json[JSON]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/jsonnet[Jsonnet]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/properties[Properties]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/spdx[Spdx]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/toml[TOML]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/vcl[VCL]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/xml[XML]
- https://github.com/ilmoraunio/conjtest/tree/main/examples/yaml[YAML]
